task prepareEnvironment(type: Exec) {
    description 'Copying configuration JSON to the connected device'
    def adb = findAdb()
    def configuration = fetchConfiguration()

    //Copy configuration file on device
    commandLine adb, 'push', getJsonFilePath(configuration), '/sdcard/'
}

private def findAdb() {
    def root = System.env.ANDROID_HOME ?: extractFromLocalProperties()
    return "$root/platform-tools/adb"
}

def extractFromLocalProperties() {
    return loadPropertiesFile("", "local.properties").getProperty("sdk.dir")
}

private def loadPropertiesFile(String subdir, String fileName) {
    def localProperties = fetchFile(project.rootDir, subdir + fileName)
    Properties properties = new Properties()
    localProperties.withInputStream {
        instr -> properties.load(instr)
    }
    return properties
}

private static def fetchFile(File rootDir, String path) {
    def file = new File(rootDir, path)
    if(!file.exists()) {
        throw new StopExecutionException("$path file is missing!")
    }
    return file
}

private def fetchConfiguration() {
    return loadPropertiesFile("configuration/", "configuration.properties")
}

private def getJsonFilePath(Properties configuration) {
    def path = configuration.getProperty("ide.json.path")
    def fileName = configuration.getProperty("file.json.name")
    def jsonFile = fetchFile(project.rootDir, "$path/$fileName")
    return jsonFile.absolutePath
}
